# cmake/pangolin_vendor.cmake
include(ExternalProject)

set(PANGOLIN_SRC_DIR "${CMAKE_CURRENT_LIST_DIR}/../third_party/Pangolin")
set(PANGOLIN_BUILD_DIR "${CMAKE_BINARY_DIR}/_deps/pangolin-build")
set(_PANGOLIN_PATCH_DIR "${CMAKE_CURRENT_LIST_DIR}/../patches/pangolin")
set(_PANGOLIN_PATCH_FILE "${_PANGOLIN_PATCH_DIR}/0001-dedup-fonts.patch")
file(SHA256 "${_PANGOLIN_PATCH_FILE}" _PANGOLIN_PATCH_SHA256)
set_property(DIRECTORY APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS "${_PANGOLIN_PATCH_FILE}")
set(_PANGOLIN_BUILD_TYPE "Release")
if(CMAKE_BUILD_TYPE)
  set(_PANGOLIN_BUILD_TYPE "${CMAKE_BUILD_TYPE}")
endif()

set(_PANGO_C_FLAGS "")
set(_PANGO_CXX_FLAGS "")
set(_PANGO_EXE_LINKER_FLAGS "")
set(_PANGO_SHARED_LINKER_FLAGS "")
if(DEFINED ORBROS_SANITIZER_FLAGS AND NOT "${ORBROS_SANITIZER_FLAGS}" STREQUAL "")
  set(_PANGO_C_FLAGS "-DCMAKE_C_FLAGS=${ORBROS_SANITIZER_FLAGS}")
  set(_PANGO_CXX_FLAGS "-DCMAKE_CXX_FLAGS=${ORBROS_SANITIZER_FLAGS}")
  set(_PANGO_EXE_LINKER_FLAGS "-DCMAKE_EXE_LINKER_FLAGS=${ORBROS_SANITIZER_FLAGS}")
  set(_PANGO_SHARED_LINKER_FLAGS "-DCMAKE_SHARED_LINKER_FLAGS=${ORBROS_SANITIZER_FLAGS}")
endif()
if(ORBROS_SHUTDOWN_TRACE)
  if(_PANGO_CXX_FLAGS)
    string(APPEND _PANGO_CXX_FLAGS " -DORBROS_SHUTDOWN_TRACE=1")
  else()
    set(_PANGO_CXX_FLAGS "-DCMAKE_CXX_FLAGS=-DORBROS_SHUTDOWN_TRACE=1")
  endif()
endif()

ExternalProject_Add(pangolin_ep
  SOURCE_DIR "${PANGOLIN_SRC_DIR}"
  BINARY_DIR "${PANGOLIN_BUILD_DIR}"
  PATCH_COMMAND
    ${CMAKE_COMMAND}
    -DSOURCE_DIR=${PANGOLIN_SRC_DIR}
    -DPATCH_FILE=${_PANGOLIN_PATCH_FILE}
    -DPATCH_LABEL=0001-dedup-fonts.patch@${_PANGOLIN_PATCH_SHA256}
    -P ${CMAKE_CURRENT_LIST_DIR}/apply_git_patch_idempotent.cmake
  CMAKE_ARGS
  -DCMAKE_BUILD_TYPE=${_PANGOLIN_BUILD_TYPE}
  -DCMAKE_POSITION_INDEPENDENT_CODE=ON
  -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
  -DBUILD_PANGOLIN_PYTHON=OFF
  ${_PANGO_C_FLAGS}
  ${_PANGO_CXX_FLAGS}
  ${_PANGO_EXE_LINKER_FLAGS}
  ${_PANGO_SHARED_LINKER_FLAGS}
  BUILD_COMMAND ${CMAKE_COMMAND} --build . --config ${_PANGOLIN_BUILD_TYPE} -j
  INSTALL_COMMAND ${CMAKE_COMMAND} --build . --target install --config ${_PANGOLIN_BUILD_TYPE}
  UPDATE_COMMAND ""
)

# expose where it will install
set(Pangolin_DIR "${CMAKE_INSTALL_PREFIX}/lib/cmake/Pangolin" CACHE PATH "" FORCE)
