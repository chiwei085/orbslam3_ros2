# cmake/orbslam3_vendor.cmake
include(ExternalProject)

# use build script instead
get_filename_component(_PKG_ROOT "${CMAKE_CURRENT_LIST_DIR}/.." ABSOLUTE)
set(_SCRIPT "${_PKG_ROOT}/third_party/build_orbslam3.py")
set(_ORB_VENDOR_BUILD_TYPE "Release")
if(CMAKE_BUILD_TYPE)
  set(_ORB_VENDOR_BUILD_TYPE "${CMAKE_BUILD_TYPE}")
endif()
set(_ORB_VENDOR_BUILD_JOBS 4)
if(ORBROS_ENABLE_ASAN_UBSAN OR ORBROS_ENABLE_ASAN OR ORBROS_ENABLE_UBSAN)
  set(_ORB_VENDOR_BUILD_JOBS 1)
endif()
set(_ORB_ENABLE_SHUTDOWN_TRACE_ENV 0)
if(ORBROS_SHUTDOWN_TRACE)
  set(_ORB_ENABLE_SHUTDOWN_TRACE_ENV 1)
endif()

ExternalProject_Add(orbslam3_ep
  DEPENDS pangolin_ep
  SOURCE_DIR "${_PKG_ROOT}/third_party/ORB_SLAM3"
  BINARY_DIR "${CMAKE_BINARY_DIR}/_deps/orbslam3-dummy"
  CONFIGURE_COMMAND ""
  BUILD_COMMAND
  ${CMAKE_COMMAND} -E env
  ORB_BUILD_JOBS=${_ORB_VENDOR_BUILD_JOBS}
  ORB_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
  ORB_CMAKE_GENERATOR=Ninja
  ORB_BUILD_TYPE=${_ORB_VENDOR_BUILD_TYPE}
  ORB_EXTRA_CMAKE_C_FLAGS=${ORBROS_SANITIZER_FLAGS}
  ORB_EXTRA_CMAKE_CXX_FLAGS=${ORBROS_SANITIZER_FLAGS}
  ORB_EXTRA_CMAKE_EXE_LINKER_FLAGS=${ORBROS_SANITIZER_FLAGS}
  ORB_EXTRA_CMAKE_SHARED_LINKER_FLAGS=${ORBROS_SANITIZER_FLAGS}
  ORB_ENABLE_SHUTDOWN_TRACE=${_ORB_ENABLE_SHUTDOWN_TRACE_ENV}
  python3 ${_SCRIPT}
  INSTALL_COMMAND ""
  UPDATE_COMMAND ""
)
